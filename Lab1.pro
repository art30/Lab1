/*      			РЕЗУЛЬТАТЫ ДОПРОСА
    На допрос вызваны четверо подозреваемых в ограблении: А, В, С, Д.
Неопровержимыми уликами доказано, что по крайней мере один из них виновен и что никто,
кроме А, В, С, Д, в ограблении не участвовал.

Кроме того, удалось установить следующее:
    1. А безусловно не виновен.
    2. Если В виновен, то у него был ровно один сообщник.
    3. Если С виновен, то у него было ровно два сообщника.
Инспектору Крэгу было особенно важно узнать, виновен или не виновен Д, так как Д был 
опасным преступником.

К счастью, приведенных выше удалось_установитьов достаточно, чтобы установить виновность 
или невиновность подозреваемого.
Виновен или не виновен Д?                               */

DOMAINS
  имя = а; в; с; д
  виновность = symbol
  
  имена = имя*
  виновности = виновность*
  
  подозреваемый = подозреваемый(имя, виновность)
  гипотеза = подозреваемый*
PREDICATES
% Вспомогательные процедуры
    nondeterm входит_в(имя, имена)
    nondeterm входит_в(виновность, виновности)
    nondeterm входит_в(подозреваемый, гипотеза)
    
    nondeterm генерация_гипотезы(гипотеза)
    nondeterm проверка_гипотезы(гипотеза)
    nondeterm удалось_установить(integer, гипотеза)
    nondeterm поиск_решения(виновность)
    
    nondeterm решение_без_повторов(виновности, виновности, виновности)
    nondeterm решения(виновность)
CLAUSES
% Вспомогательные процедуры
	входит_в(Name,[Name|_]).
	входит_в(Name,[_|Хвост]):-входит_в(Name,Хвост).
      
    решения(УникальноеРешение):-
        findall(Решение, поиск_решения(Решение), ВсеРешения),
        решение_без_повторов(ВсеРешения, [], УникальныеРешения),
        входит_в(УникальноеРешение, УникальныеРешения).
    
% 1. А безусловно не виновен.
    удалось_установить(1, Гипотеза):-
        входит_в(подозреваемый(а, невиновен), Гипотеза).
% 2. Если В виновен, то у него был ровно один сообщник.
    удалось_установить(2, Гипотеза):-
        входит_в(подозреваемый(в, виновен), Гипотеза),
        findall(Виновный, входит_в(подозреваемый(Виновный, виновен), Гипотеза), Виновные),
        Виновные = [_, _]; % два виновных
        % иначе
        входит_в(подозреваемый(в, невиновен), Гипотеза).
% 3. Если С виновен, то у него было ровно два сообщника. 
    удалось_установить(3, Гипотеза):-
        входит_в(подозреваемый(с, виновен), Гипотеза),
        findall(Виновный, входит_в(подозреваемый(Виновный, виновен), Гипотеза), Виновные),
        Виновные = [_, _, _]; % три виновных
        % иначе
        входит_в(подозреваемый(с, невиновен), Гипотеза).
      
    проверка_гипотезы(Гипотеза):-
        % по крайней мере один из них виновен 
        входит_в(подозреваемый(_Виновный, виновен), Гипотеза),
        удалось_установить(1, Гипотеза),
        удалось_установить(2, Гипотеза),
        удалось_установить(3, Гипотеза).

    генерация_гипотезы(Гипотеза):-
        ВариантыВиновности = [виновен,невиновен],

        входит_в(ВиновностьА, ВариантыВиновности),
        входит_в(ВиновностьВ, ВариантыВиновности),
        входит_в(ВиновностьС, ВариантыВиновности),
        входит_в(ВиновностьД, ВариантыВиновности),
    
        
        Гипотеза = [
        подозреваемый(а, ВиновностьА),
        подозреваемый(в, ВиновностьВ),
        подозреваемый(с, ВиновностьС),
        подозреваемый(д, ВиновностьД)
        ].
     
    поиск_решения(ВиновностьД):-
        генерация_гипотезы(Гипотеза),
    проверка_гипотезы(Гипотеза),
% Инспектору Крэгу было особенно важно узнать, виновен или не виновен Д,  
    входит_в(подозреваемый(д, ВиновностьД), Гипотеза).

% Постусловие. Если исходный список пустой, то возвращаем формирумый список как результат 
	решение_без_повторов([], Накопитель, СписокБезПовторов):-
		СписокБезПовторов = Накопитель.
% Если текущий элемент списка уже есть в формируемом списке (Накопитель) , то переходим к проверке
% остальных элементов     
	решение_без_повторов([ПервыйЭлемент|ОстальныеЭлементы], Накопитель, СписокБезПовторов):-
		входит_в(ПервыйЭлемент, Накопитель), 
		решение_без_повторов(ОстальныеЭлементы, Накопитель, СписокБезПовторов).
% Если текущий элемент списка отсутствует в формируемом списке (Накопитель), то добавляем его
% в Накопитель и переходим к проверке остальных элементов      
	решение_без_повторов([ПервыйЭлемент|ОстальныеЭлементы], Накопитель, СписокБезПовторов):-
		NOT(входит_в(ПервыйЭлемент, Накопитель)),
		решение_без_повторов(ОстальныеЭлементы, [ПервыйЭлемент|Накопитель], СписокБезПовторов).
GOAL 
    решения(Подозреваемый_Д).